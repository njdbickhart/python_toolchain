import os
import subprocess as sp
from subprocess import PIPE, STDOUT
import pysam

shell.executable("/usr/bin/bash")

configfile : "default.json"

os.makedirs("logs", exist_ok=True)
os.makedirs("mapped", exist_ok=True)

rule all:
    input:
        "final/summary_table.tab",
        expand("final/ideogram_errors.{asm}.pdf", asm = config["assembly"].keys()),
        expand("final/ideogram_upperq_error_windows.{asm}.bed", asm = config["assembly"].keys()),
        expand("merqury/{asm}/mer.spectra-asm.{postfix}.pdf", asm = config["assembly"].keys(), postfix=["fl", "ln", "st"])


rule align_reads:
    input:
        fq1 = lambda wildcards: config["samples"][wildcards.sample][0],
        fq2 = lambda wildcards: config["samples"][wildcards.sample][1],
        fasta = lambda wildcards: config["assembly"][wildcards.asm]
    output:
        temp("mapped/{asm}/{sample}.bam")
    log:
        "logs/{asm}/{sample}_bwa.log"
    threads: 8
    conda:
        "envs/base.yaml"
    shell:
        """
        bwa mem -R '@RG\\tID:{wildcards.sample}\\tSM:{wildcards.sample}\\tPL:ILLUMINA' -t {threads} -M {input.fasta} {input.fq1} {input.fq2} | samtools sort -o {output}  - >> {log} 2>&1
        samtools index {output}
        """

rule merge_bams:
    input:
        expand("mapped/{{asm}}/{sample}.bam", sample=config["samples"])
    output:
        "mapped/{asm}/merged.bam",
        "mapped/{asm}/merged.bam.bai"
    threads: 8
    conda:
        "envs/base.yaml"
    shell:
        """
        samtools merge -@ {threads} {output[0]} {input}
        samtools index {output[0]}
        """


rule freebayes:
    input:
        ref=lambda wildcards: config["assembly"][wildcards.asm],
        samples="mapped/{asm}/merged.bam",
        indexes="mapped/{asm}/merged.bam.bai"
    output:
        "calls/{asm}/merged_freebayes.vcf"
    log:
        "logs/{asm}/freebayes.log"
    conda:
        "envs/freebayes.yaml"
    params:
        extra="-C 2 -0 -O -q 20 -z 0.10 -E 0 -X -u -p 2 -F 0.75",
        chunksize=1000000
    threads: 10
    script:
        "scripts/freebayes_lift.py"



rule frc_align:
    input:
        ref=lambda wildcards: config["assembly"][wildcards.asm],
        samples="mapped/{asm}/merged.bam"
    output:
        features="calls/{asm}/merged_frc.txt_Features.txt",
        frc="calls/{asm}/merged_frc.txt_FRC.txt"
    params:
        outprefix="calls/{asm}/merged_frc.txt"
    shell:
        """
        module load frc_align
        FRC --pe-sam {input.samples} --output {params.outprefix}
        """

rule lumpy:
    input:
        samples="mapped/{asm}/merged.bam"
    output:
        "calls/{asm}/merged_lumpy.vcf"
    conda:
        "envs/lumpy.yaml"
    log:
        "logs/{asm}/lumpy.log"
    shell:
        """
        lumpyexpress -B {input.samples} -o {output} -v 2> {log}
        """

rule samtools_depth:
    input:
        samples="mapped/{asm}/merged.bam"
    output:
        samdepth="calls/{asm}/merged_depth.txt"
    conda:
        "envs/depth.yaml"
    params:
        threshold = 3
    script:
        "scripts/depth_estimate.py"

rule qv_estimate:
    input:
        vcf="calls/{asm}/merged_freebayes.vcf",
        depth="calls/{asm}/merged_depth.txt"
    output:
        "calls/{asm}/qv_value.txt"
    shell:
        """
        bash {workflow.basedir}/scripts/qv_estimate.sh {input.depth} {input.vcf} {output[0]}
        """


def getFlatFastqList(wildcards, config):
    data = list()
    for i in range(2):
        data.append(config["samples"][wildcards.sample][i])
    return data

rule meryl_hapmer:
    input:
        lambda wildcards : getFlatFastqList(wildcards, config)
    output:
        directory("mapped/{sample}_1.meryl"),
        directory("mapped/{sample}_2.meryl")
    threads: 24
    params:
        k = 21,
        extra = "threads=24 memory=48"
    shell:
        """
        module load merqury/1.0
        meryl k={params.k} count output {output[0]} {params.extra} {input[0]}
        meryl k={params.k} count output {output[1]} {params.extra} {input[1]}
        """

rule meryl_merge:
    input:
        expand("mapped/{sample}_{lane}.meryl", sample=config["samples"], lane=["1", "2"])
    output:
        directory("mapped/meryl_db.meryl"),
    threads: 10
    params:
        extra = "cpus=10 memory=48g"
    shell:
        """
        module load merqury/1.0
        meryl union-sum output {output} {input}
        """

rule run_merqury:
    input:
        mdb = "mapped/meryl_db.meryl",
        fasta = lambda wildcards: config["assembly"][wildcards.asm]
    output:
        dist_only = "merqury/{asm}/mer.dist_only.hist",
        qv = "merqury/{asm}/mer.qv",
        spectraasm = "merqury/{asm}/mer.spectra-asm.hist",
        f1 = "merqury/{asm}/mer.spectra-asm.fl.pdf",
        ln = "merqury/{asm}/mer.spectra-asm.ln.pdf",
        st = "merqury/{asm}/mer.spectra-asm.st.pdf",
        complete = "merqury/{asm}/completeness.stats"
    params:
        outbase = "mer",
        pdfbase = "mer.spectra-asm"
    threads: 20
    shell:
        """
        module load merqury/1.0
        merqury.sh {input.mdb} {input.fasta} {params.outbase}
        mv mer.* ./merqury/
        mv completeness.stats ./merqury/completeness.stats
        /software/7/apps/merqury/1.0/plot/plot_spectra_cn.R -f {output.spectraasm} -o {params.pdfbase} -z {output.dist_only} --pdf
        mv mer.*.pdf ./merqury/
        """

rule run_busco:
    input:
        lambda wildcards: config["assembly"][wildcards.asm]
    output:
        "busco/{asm}/busco_summary.txt",
    log:
        "logs/{asm}/run_busco.log"
    threads: 20
    conda:
        "envs/busco.yaml"
    params:
        mode="genome",
        lineage_path=config["buscoLineage"],
        asm = wildcards.asm,
        # optional parameters
        extra=""
    script:
        "scripts/busco_lift.py"

rule summary_table:
    input:
        merqv = expand("merqury/{asm}/mer.qv", asm=config["assembly"].keys()),
        complete = expand("merqury/{asm}/completeness.stats", asm=config["assembly"].keys()),
        busco = expand("busco/{asm}/busco_summary.txt", asm=config["assembly"].keys()),
        snpqv = expand("calls/{asm}/qv_value.txt", asm=config["assembly"].keys()),
        bams=expand("mapped/{asm}/merged.bam", asm=config["assembly"].keys()),
        features=expand("calls/{asm}/merged_frc.txt_Features.txt", asm=config["assembly"].keys()),
        lumpy = expand("calls/{asm}/merged_lumpy.vcf", asm=config["assembly"].keys())
    output:
        table = "final/summary_table.tab"
    script:
        "scripts/summary_table.py"

rule ideogram_plot:
    input:
        features="calls/{asm}/merged_frc.txt_Features.txt",
        bams="mapped/{asm}/merged.bam"
    output:
        ideogram="final/ideogram_errors.{asm}.pdf",
        bed="final/ideogram_upperq_error_windows.{asm}.bed"
    params:
        chrthresh = 30
    conda:
        "envs/base.yaml"
    shell:
        'python {workflow.basedir}/scripts/ideogram_feature_plot.py -f {input.features}'
        ' -b {input.bams} -o {output.ideogram} -e {output.bed} -t {params.chrthresh}'

# Begin comparative plots
if len(config["assembly"].keys()) > 1:
    rule plot_frc:
        input:
            expand("calls/{asm}/merged_frc.txt_FRC.txt", asm=config["assembly"].keys())
        output:
            plot = "final/combined_frc_plot.pdf"
        conda:
            "envs/base.yaml"
        script:
            "scripts/asm_frc_plot.py"

#rule ngx_plot (see assemblathon2 manuscript)

#rule variant_size_histos (see assemblytics)
